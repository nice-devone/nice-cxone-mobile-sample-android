import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

plugins {
    id "org.jetbrains.kotlin.android" version "1.7.21" apply false
    id "com.android.application" version "7.3.1" apply false
    id "com.google.gms.google-services" version "4.3.10" apply false
    id "androidx.navigation.safeargs" version "2.5.3" apply false
    id "io.gitlab.arturbosch.detekt" version "1.21.0"
    id "com.google.dagger.hilt.android" version "2.44" apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task reportMerge(type: ReportMergeTask) {
    output = rootProject.layout.buildDirectory.file("reports/detekt/merge.sarif")
}

subprojects {
    afterEvaluate {
        plugins.withType(DetektPlugin) {
            tasks.withType(Detekt) { detektTask ->
                finalizedBy(reportMerge)

                reportMerge.configure { mergeTask ->
                    mergeTask.input.from(detektTask.sarifReportFile)
                }
            }
        }
    }
}
